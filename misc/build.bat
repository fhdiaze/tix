@echo off

setlocal enabledelayedexpansion

REM Captured now because "shift" (used below during argument parsing) can end up
REM shifting %0 too once all positional args are consumed, which would corrupt %~dp0.
set "ScriptDir=%~dp0"

set "BuildMode=debug"
set "Architecture=x64"
set "LiveBuild=0"
set "AppFileName=app"
set "SysFileName=sys_win"
set "SysFilePath=./src/%SysFileName%.c"
set "AppFilePath=./src/%AppFileName%.c"
set "Outdir=./bin"
set "Datadir=./data"
set "OutAppFileName=tix_app"
set "OutSysFileName=tix_win"
set "OutSysFilePath=%Outdir%/%OutSysFileName%.exe"
set "OutAppFilePath=%Outdir%/%OutAppFileName%.dll"
set "FlagsFile=%ScriptDir%../compile_flags.txt"
set "DebugFlags=-g -gcodeview -O0 -DDEBUG -Wl,/DEBUG:FULL -fms-runtime-lib=static_dbg"
@REM set "DebugFlags=!DebugFlags! -fsanitize=address -fno-omit-frame-pointer"
set "ReleaseFlags=-O3 -DNDEBUG -flto -Wl,/opt:ref -Wl,/opt:icf -fms-runtime-lib=static"
set "Flags="
set "AppFlags=-shared -Wl,/MAP:%Outdir%/%OutAppFileName%.map,/MAPINFO:EXPORTS -Wl,/PDB:%Outdir%/%OutAppFileName%_%random%.pdb"
set "SysFlags=-mavx2 -luser32 -lgdi32 -lwinmm -ldwmapi -Wl,/subsystem:windows -Wl,/MAP:%Outdir%/%OutSysFileName%.map,/MAPINFO:EXPORTS"

:parse_args

if "%~1"=="" goto :done_args
if /i "%~1"=="/m" (
    if "%~2"=="" (
        echo Error: /m requires a value. & exit /b 1
    )
    set "BuildMode=%~2" & shift & shift & goto :parse_args
)
if /i "%~1"=="/a" (
    if "%~2"=="" (
        echo Error: /a requires a value. & exit /b 1
    )
    set "Architecture=%~2" & shift & shift & goto :parse_args
)
if /i "%~1"=="/lb" (
    set "LiveBuild=1"       & shift        & goto :parse_args
)
echo Error: Unknown argument "%~1".
exit /b 1

:done_args

if /i not "%BuildMode%"=="debug" if /i not "%BuildMode%"=="release" (
    echo Error: Invalid build mode "%BuildMode%". Must be "debug" or "release".
    exit /b 1
)

if /i not "%Architecture%"=="x86" if /i not "%Architecture%"=="x64" (
    echo Error: Invalid architecture "%Architecture%". Must be "x86" or "x64".
    exit /b 1
)

echo.
REM VCToolsInstallDir is unique to the MSVC dev environment, unlike LIB which other tools (e.g. curl) may already set
if not defined VCToolsInstallDir (
    set "VsWhere=%ProgramFiles(x86)%\Microsoft Visual Studio\Installer\vswhere.exe"
    if not exist "!VsWhere!" (
        echo Error: vswhere.exe not found. Install Visual Studio Build Tools, or run this script from a Developer Command Prompt.
        exit /b 1
    )

    set "VsInstallPath="
    for /f "usebackq tokens=*" %%i in (`"!VsWhere!" -latest -products * -requires Microsoft.VisualStudio.Component.VC.Tools.x86.x64 -property installationPath`) do (
        set "VsInstallPath=%%i"
    )

    if not defined VsInstallPath (
        echo Error: no Visual Studio installation with the C++ build tools component was found.
        exit /b 1
    )

    echo Setting up the MSVC build environment for %Architecture%...
    call "!VsInstallPath!\VC\Auxiliary\Build\vcvarsall.bat" %Architecture%
    if errorlevel 1 (
        echo Error: vcvarsall.bat failed to initialize the build environment.
        exit /b 1
    )
)
echo.

if not exist "%Outdir%" (
    echo Creating %Outdir%...
    mkdir "%Outdir%"
) else (
    echo Cleaning %Outdir%...
    if %LiveBuild% equ 1 (
        del /q "%Outdir%\*.txt" 2>nul
        del /q "%Outdir%\*.pdb" 2>nul
    ) else (
        del /s /q "%Outdir%\*" 2>nul
    )
)

if not exist "%Datadir%" (
    echo Creating %Datadir%...
    mkdir "%Datadir%"
) else (
    echo Cleaning %Datadir%...
    del /q "%Datadir%\log.txt" 2>nul
)

REM Read flags from file (path is relative to this script's location, not the caller's cwd)
for /f "usebackq tokens=*" %%A in ("%FlagsFile%") do (
    set "line=%%A"
    set "line=!line: =!"
    if not "!line!"=="" if not "!line:~0,2!"=="//" (
        set "Flags=!Flags! %%A"
    )
)

if "%Architecture%"=="x86" (
    set "Flags=!Flags! -m32"
    echo Building for 32-bit ^(x86^)...
) else (
    set "Flags=!Flags! -m64"
    echo Building for 64-bit ^(x64^)...
)

if "%BuildMode%"=="debug" (
    set "Flags=!Flags! %DebugFlags%"
    echo Building in DEBUG mode...
) else (
    set "Flags=!Flags! %ReleaseFlags%"
    echo Building in RELEASE mode...
)

set "AppFlags=!AppFlags! !Flags!"

echo Building %OutAppFilePath% ...
echo.
echo clang !AppFlags! %AppFilePath% -o %OutAppFilePath%
echo.
echo Waiting for pdb file>"%Outdir%\lock.tmp"
echo.

clang !AppFlags! %AppFilePath% -o %OutAppFilePath%
set "AppBuildErrorLevel=%errorlevel%"

del /q "%Outdir%\lock.tmp" 2>nul

if %AppBuildErrorLevel% neq 0 (
    echo Building %OutAppFilePath% failed!
    exit /b %AppBuildErrorLevel%
)

echo.
echo Building %OutAppFilePath% succeeded!
echo.

echo ================================================================================
echo.

if %LiveBuild% equ 1 (
    echo Live build completed. Skipping system build.
    exit /b 0
)

set "SysFlags=!SysFlags! !Flags!"

echo Building %OutSysFilePath% ...
echo.
echo clang !SysFlags! %SysFilePath% -o %OutSysFilePath%
echo.

clang !SysFlags! %SysFilePath% -o %OutSysFilePath%

if errorlevel 1 (
    echo Building %OutSysFilePath% failed!
    exit /b %errorlevel%
)

echo.
echo Building %OutSysFilePath% succeeded!
